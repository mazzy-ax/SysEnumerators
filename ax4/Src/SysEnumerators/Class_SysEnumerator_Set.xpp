//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysEnumerator_Set extends xSysEnumerator
{
    Set base;
    SetEnumerator baseEnumerator;

    public Set base()
    {
        return base;
    }

    public SetEnumerator baseEnumerator()
    {
        return baseEnumerator;
    }

    protected Set baseOrDefault(Set _base = null)
    {
        if( !_base )
        {
            _base = new Set(Types::Integer);
        }

        return _base;
    }

    protected boolean begin()
    {
        boolean ret = super() && this.next();

        return ret;
    }

    public anytype current()
    {
        anytype ret = baseEnumerator.current();

        return ret;
    }

    public anytype currentIdx()
    {
        return this.current();
    }

    public anytype currentKey()
    {
        return this.current();
    }

    public anytype currentValue()
    {
        return this.current();
    }

    protected boolean currentValueProvided()
    {
        return true;
    }

    public int64 estimatedIterations(int64 estimatedIterations = -1)
    {
        int64 ret = this.base().elements();

        return ret;
    }

    public void new(Set _base = null)
    {
        base = this.baseOrDefault(_base);
        baseEnumerator = base.getEnumerator();
        super();
    }

    protected boolean next()
    {
        boolean ret = super() && baseEnumerator.moveNext();

        return ret;
    }

    public void reset()
    {
        baseEnumerator.reset();
        super();
    }

    public static SysEnumerator_Set construct(Set base = null)
    {
        SysEnumerator_Set enumerator = new SysEnumerator_Set(base);

        return enumerator;
    }
}