//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysRangeDate extends SysRange implements ISysRange
{
    date start;
    date end;

    public SysRangeDate clone()
    {
        SysRangeDate range = SysRangeDate::construct(this.start(), this.end());

        return range;
    }

    public boolean contains(Date value)
    {
        boolean ret = (this.start() <= value && value <= this.end());
        return ret;
    }

    public boolean cover(Date value)
    {
        boolean ret = (this.start() < value && value < this.end());
        return ret;
    }

    // переполнения не бывает в  [datenull, datemax]
    public int distance()
    {
        int ret = this.end() - this.start();

        return ret;
    }

    public date end()
    {
        return end;
    }

    public boolean equal(SysRangeDate range)
    {
        boolean ret = range && (this.start() == range.start()) && (this.end() == range.end());
        return ret;
    }

    public boolean in(SysRangeDate range)
    {
        boolean ret = range && (range.start() <= this.start()) && (this.end() <= range.end());
        return ret;
    }

    public boolean isBoundary(Date value)
    {
        return (value == this.start()) || (value == this.end());
    }

    public void new(date _start = dateNull(), date _end = dateMax())
    {
        start = _start;
        end = _end;
    }

    public SysRangeDate normalize()
    {
        SysRangeDate range;

        if( this.reversed() )
        {
            range = this.reverse();
        }
        else
        {
            range = this.clone();
        }

        return range;
    }

    public SysRangeDate reverse()
    {
        SysRangeDate range = SysRangeDate::construct(this.end(), this.start());

        return range;
    }

    public boolean reversed()
    {
        boolean ret = this.start() > this.end();
        return ret;
    }

    public date start()
    {
        return start;
    }

    public str toString()
    {
        // SysQuery::range() is not nice
        return strfmt('[%1..%2]', this.start(), this.end());
    }

    public static SysRangeDate construct(Date start = dateNull(), date end = dateMax())
    {
        SysRangeDate range = new SysRangeDate(start, end);

        return range;
    }
}