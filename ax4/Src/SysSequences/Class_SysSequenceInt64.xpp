//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceInt64 extends SysRangeInt64 implements ISysSequence, Enumerable
{
    int64 step;

    public SysSequenceInt64 clone()
    {
        SysSequenceInt64 range = SysSequenceInt64::construct(this.start(), this.end(), this.step());

        return range;
    }

    public boolean equal(SysSequenceInt64 sequence)
    {
        boolean ret = super(sequence) && (this.step() == sequence.step());
        return ret;
    }

    public int64 estimatedIterations()
    {
        int64 ret = int64Max();
        int64 distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0);

        if( overflow )
        {
            ret = int64Max();
        }
        else if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = distance + 1;
        }
        else
        {
            ret = distance div this.step() + 1;
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromSequenceInt64(this);
    }

    public int64 getNext(int64 value)
    {
        return value + this.step();
    }

    public void new(int64 _start = 1, int64 _end = int64Max(), int64 _step = 1)
    {;
        Debug::assert(_step != 0);

        super(_start, _end);
        step = _step;
    }

    public SysSequenceInt64 reverse()
    {
        SysSequenceInt64 sequence = SysSequenceInt64::construct(this.end(), this.start(), this.reverseStep());

        return sequence;
    }

    public int64 reverseStep()
    {
        return -step;
    }

    public int64 step()
    {
        return step;
    }

    public str toString()
    {
        return strfmt('%1:[%2]', this.step(), super());
    }

    public static SysSequenceInt64 construct(int64 _start = 1, int64 _end = int64Max(), int64 _step = 1)
    {
        SysSequenceInt64 sequence = new SysSequenceInt64(_start, _end, _step);

        return sequence;
    }
}