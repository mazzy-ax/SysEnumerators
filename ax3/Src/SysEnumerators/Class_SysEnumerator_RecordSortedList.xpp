//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysEnumerator_RecordSortedList extends xSysEnumerator
{
    RecordSortedList base;
    Common record;
    Common startFrom;

    public RecordSortedList base()
    {
        return base;
    }

    protected RecordSortedList baseOrDefault(RecordSortedList _base = null)
    {
        TmpTree tmp;

        if( !_base )
        {
            _base = new RecordSortedList(tmp.TableId);
        }

        return _base;
    }

    protected boolean begin()
    {
        boolean ret = super();

        if( ret )
        {
            if( this.parmStartFrom() == null )
            {
                ret = this.base().first(record);
            }
            else
            {
                record.data(startFrom);
                ret = this.base().find(record);
            }
        }

        return ret;
    }

    public anytype current()
    {
        return record;
    }

    public anytype currentIdx()
    {
        return this.current();
    }

    public anytype currentKey()
    {
        return this.current();
    }

    public anytype currentValue()
    {
        return this.current();
    }

    protected boolean currentValueProvided()
    {
        return true;
    }

    public int estimatedIterations(int estimatedIterations = -1)
    {
        int ret = this.base().len();

        return ret;
    }

    public void new(RecordSortedList _base = null, Common _startFrom = null)
    {
        base = this.baseOrDefault(_base);
        startFrom = _startFrom;
        super();
    }

    protected boolean next()
    {
        boolean ret = super() && this.base().next(record);

        return ret;
    }

    public common parmStartFrom(common _startFrom = startFrom)
    {
        startFrom = _startFrom;
        return startFrom;
    }

    public common record()
    {
        return record;
    }

    public static SysEnumerator_RecordSortedList construct(RecordSortedList base, Common startFrom = null)
    {
        SysEnumerator_RecordSortedList enumerator = new SysEnumerator_RecordSortedList(base, startFrom);

        return enumerator;
    }

}