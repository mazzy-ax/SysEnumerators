//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysEnumerator_SysOperationProgress extends SysEnumerator_Int64
{
    #AviFiles

    SysOperationProgress base;

    public SysOperationProgress base()
    {
        return base;
    }

    protected SysOperationProgress baseOrDefault(SysOperationProgress _base = null)
    {
        if( _base == null )
        {
            _base = SysOperationProgress::newGeneral(#AviStopwatch, '', 0);
        }

        return _base;
    }

    protected boolean begin()
    {
        boolean ret = super() && this.next();

        return ret;
    }

    public int64 estimatedIterations(int64 estimatedIterations = -1)
    {
        int64 ret;

        if( estimatedIterations >= 0 )
        {
            this.base().setTotal(estimatedIterations);
        }

        ret = this.base().getTotal();

        if( ret == 0 )
        {
            ret = super();
        }

        return ret;
    }

    public boolean moveNext()
    {
        boolean ret = super();

        if( !ret )
        {
            this.base().reset(); // hide progress
        }

        return ret;
    }

    public void new(SysOperationProgressBase _base = null)
    {
        base = this.baseOrDefault(_base);

        if( base.getTotal() )
        {
            super(SysSequenceInt64::construct(_base.getCount_RU(), base.getTotal()));
        }
        else
        {
            super();
        }
    }

    protected boolean next()
    {
        boolean ret = super();

        if( ret )
        {
            this.base().incCount();
        }

        return ret;
    }

    public void reset()
    {
        this.base().reset();
        super();
    }

    public static SysEnumerator_SysOperationProgress construct(SysOperationProgress base = null)
    {
        SysEnumerator_SysOperationProgress enumerator = new SysEnumerator_SysOperationProgress(base);

        return enumerator;
    }

    public static SysEnumerator_SysOperationProgress newGeneral(
        Filename    animation = #AviStopwatch,
        Caption     caption = '',
        int64       total = 0)
    {
        SysEnumerator_SysOperationProgress enumerator = SysEnumerator_SysOperationProgress::construct();
        SysOperationProgress progress = enumerator.base();
    ;
        progress.setAnimation(animation);
        progress.setCaption(caption);
        progress.setTotal(total);

        return enumerator;

    }
}