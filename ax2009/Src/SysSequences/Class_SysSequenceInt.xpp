//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
class SysSequenceInt extends SysRangeInt implements ISysSequence, Enumerable
{
	int step;

    public SysSequenceInt clone()
    {
        SysSequenceInt range = SysSequenceInt::construct(this.start(), this.end(), this.step());
    
        return range;
    }

    public boolean equal(SysSequenceInt sequence)
    {
        boolean ret = super(sequence) && (this.step() == sequence.step());
        return ret;
    }

    public int64 estimatedIterations()
    {
        int64 ret = int64Max();
        int distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0);
    
        if( overflow )
        {
            ret = int64Max();
        }

        else if( distance == 0 )
        {
            ret = 1;
        }

        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }

        else if( step == 1 )
        {
            ret = distance + 1;
        }

        else
        {
            ret = distance div this.step() + 1;
        }

    
        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromSequenceInt(this);
    }

    public int getNext(int value)
    {
        return value + this.step();
    }

    public void new(int _start = 1, int _end = maxint(), int _step = 1)
    {;
        Debug::assert(_step != 0);
    
        super(_start, _end);
        step = _step;
    }

    public SysSequenceInt reverse()
    {
        SysSequenceInt sequence = SysSequenceInt::construct(this.end(), this.start(), -this.step());
    
        return sequence;
    }

    public int step()
    {
        return step;
    }

    public str toString()
    {
        return strfmt('%1:[%2]', this.step(), super());
    }

    public static SysSequenceInt construct(int _start = 1, int _end = maxint(), int _step = 1)
    {
        SysSequenceInt sequence = new SysSequenceInt(_start, _end, _step);
    
        return sequence;
    }
}