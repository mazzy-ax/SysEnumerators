//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
class SysSequenceAmount extends SysRangeAmount implements ISysSequence, Enumerable
{
    amount step;

    public SysSequenceAmount clone()
    {
        SysSequenceAmount range = SysSequenceAmount::construct(this.start(), this.end(), this.step());

        return range;
    }

    public boolean equal(SysSequenceAmount sequence)
    {
        boolean ret = super(sequence) && (this.step() == sequence.step());
        return ret;
    }

    public int64 estimatedIterations()
    {
        int64 ret = int64Max();
        Amount distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0.0001);

        if( overflow )
        {
            ret = int64Max();
        }
        else if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = any2int64(distance + 1.0);
        }
        else
        {
            ret = any2int64(distance / this.step() + 1.0);
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromSequence(this);
    }

    public amount getNext(amount value)
    {
        return value + this.step();
    }

    public void new(amount _start = 1, amount _end = RealMax(), amount _step = 1)
    {;
        Debug::assert(_step != 0.0);

        super(_start, _end);
        step = _step;
    }

    public SysSequenceAmount reverse()
    {
        SysSequenceAmount sequence = SysSequenceAmount::construct(this.end(), this.start(), -this.step());

        return sequence;
    }

    public amount step()
    {
        return step;
    }

    public str toString()
    {
        return strfmt('%1:[%2]', this.step(), super());
    }

    public static SysSequenceAmount construct(amount _start = 1, amount _end = RealMax(), amount _step = 1)
    {
        SysSequenceAmount sequence = new SysSequenceAmount(_start, _end, _step);

        return sequence;
    }
}