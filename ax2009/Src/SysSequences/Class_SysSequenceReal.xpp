//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
class SysSequenceReal extends SysRangeReal implements ISysSequence, Enumerable
{
	real step;

    public SysSequenceReal clone()
    {
        SysSequenceReal range = SysSequenceReal::construct(this.start(), this.end(), this.step());
    
        return range;
    }

    public boolean equal(SysSequenceReal sequence)
    {
        boolean ret = super(sequence) && (this.step() == sequence.step());
        return ret;
    }

    public int64 estimatedIterations()
    {
        int64 ret = int64Max();
        Amount distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0.00001);
    
        if( overflow )
        {
            ret = int64Max();
        }

        else if( distance == 0 )
        {
            ret = 1;
        }

        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }

        else if( step == 1 )
        {
            ret = any2int64(distance + 1.0);
        }

        else
        {
            ret = any2int64(distance / this.step() + 1.0);
        }

    
        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromToReal(this.start(), this.end(), this.step());
    }

    public real getNext(real value)
    {
        return value + this.step();
    }

    public void new(real _start = 1.0, real _end = RealMax(), real _step = 1.0)
    {;
        Debug::assert(_step != 0.0);
    
        super(_start, _end);
        step = _step;
    }

    public SysSequenceReal reverse()
    {
        SysSequenceReal sequence = SysSequenceReal::construct(this.end(), this.start(), -this.step());
    
        return sequence;
    }

    public real step()
    {
        return step;
    }

    public str toString()
    {
        return strfmt('%1:[%2]', this.step(), super());
    }

    public static SysSequenceReal construct(real _start = 1, real _end = RealMax(), real _step = 1)
    {
        SysSequenceReal sequence = new SysSequenceReal(_start, _end, _step);
    
        return sequence;
    }
}