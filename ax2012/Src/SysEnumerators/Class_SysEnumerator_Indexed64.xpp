//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public abstract class SysEnumerator_Indexed64 extends xSysEnumerator
{
    SysSequenceInt64 sequence;
    int64 currentIdx;

    public anytype currentIdx()
    {
        return currentIdx;
    }

    protected boolean currentIdxProvided()
    {
        return true;
    }

    protected int64 defaultEnd()
    {
        return 0;
    }

    protected int64 defaultStart()
    {
        return 1;
    }

    protected int64 defaultStep()
    {
        return 1;
    }

    public int64 estimatedIterations(int64 estimatedIterations = 0)
    {
        int64 ret = this.sequence().estimatedIterations();

        return ret;
    }

    protected void init()
    {
        currentIdx = this.sequence().start();
    }

    protected boolean more()
    {
        boolean ret = super();
        int64 idx;

        if( ret )
        {
            idx = this.currentIdx();
            ret = this.sequence().contains(idx);
        }

        return ret;
    }

    public void new(SysSequenceInt64 _sequence = null)
    {;
        sequence = this.sequenceOrDefault(_sequence);
        super();
    }

    protected boolean next()
    {
        int64 idx = this.currentIdx();
        currentIdx = this.sequence().getNext(idx);

        return super();
    }

    public SysSequenceInt64 sequence()
    {
        return sequence;
    }

    protected SysSequenceInt64 sequenceOrDefault(SysSequenceInt64 _sequence = null)
    {
        if( _sequence == null )
        {
            _sequence = SysSequenceInt64::construct(this.defaultStart(), this.defaultEnd(), this.defaultStep());
        }

        return _sequence;
    }
}