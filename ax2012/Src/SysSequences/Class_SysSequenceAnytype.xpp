//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceAnytype implements ISysSequence
{
    anytype start;
    anytype end;
    anytype step;

    public SysSequenceAnytype clone()
    {
        SysSequenceAnytype range = SysSequenceAnytype::construct(this.start(), this.end(), this.step());

        return range;
    }

    public boolean contains(anytype value)
    {
        boolean ret = (this.start() <= value && value <= this.end());
        return ret;
    }

    public boolean cover(anytype value)
    {
        boolean ret = (this.start() < value && value < this.end());
        return ret;
    }

    // переполнение в аксапте не возбуждает исключение, а возвращает значение с отброшенными битами
    public anytype distance()
    {
        anytype ret = this.end() - this.start();

        return ret;
    }

    public anytype end()
    {
        return end;
    }

    public boolean equal(SysSequenceAnytype sequence)
    {
        boolean ret =   sequence &&
                        this.start() == sequence.start() &&
                        this.end() == sequence.end() &&
                        this.step() == sequence.step();

        return ret;
    }

    public int64 estimatedIterations()
    {
        int64 ret = int64Max();
        anytype distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0.0001);

        if( overflow )
        {
            ret = int64Max();
        }
        else if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = any2int64(distance + 1.0);
        }
        else
        {
            ret = any2int64(distance / this.step() + 1.0);
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromSequence(this);
    }

    public anytype getNext(anytype value)
    {
        return value + this.step();
    }

    public boolean in(SysSequenceAnytype sequence)
    {
        boolean ret = sequence && (sequence.start() <= this.start()) && (this.end() <= sequence.end());

        return ret;
    }

    public boolean isBoundary(anytype value)
    {
        return (value == this.start()) || (value == this.end());
    }

    public void new(anytype _start, anytype _end, anytype _step)
    {
        start = _start;
        end = _end;
        step = _step;
    }

    public SysSequenceAnytype normalize()
    {
        SysSequenceAnytype sequence;

        if( this.reversed() )
        {
            sequence = this.reverse();
        }
        else
        {
            sequence = this.clone();
        }

        return sequence;
    }

    public SysSequenceAnytype reverse()
    {
        SysSequenceAnytype sequence = SysSequenceAnytype::construct(this.end(), this.start(), this.reverseStep());

        return sequence;
    }

    public boolean reversed()
    {
        boolean ret = this.start() > this.end();
        return ret;
    }

    public anytype reverseStep()
    {
        return 0-step;
    }

    public anytype start()
    {
        return start;
    }

    public anytype step()
    {
        return step;
    }

    public str toString()
    {
        return strfmt('[%1..%2:%3]', this.start(), this.end(), this.step());
    }

    public ISysSequence xClone()
    {
        return this.clone();
    }

    public boolean xContains(anytype value)
    {
        return this.contains(value);
    }

    public boolean xCover(anytype value)
    {
        return this.cover(value);
    }

    // переполнение в аксапте не возбуждает исключение, а возвращает значение с отброшенными битами
    public anytype xDistance()
    {
        return this.distance();
    }

    public anytype xEnd()
    {
        return this.end();
    }

    public boolean xEqual(ISysSequence range)
    {
        return this.equal(range as SysSequenceAnytype);
    }

    public anytype xGetNext(anytype value)
    {
        return this.getNext(value);
    }

    public boolean xIn(ISysSequence range)
    {
        return this.in(range as SysSequenceAnytype);
    }

    public boolean xIsBoundary(anytype value)
    {
        return this.isBoundary(value);
    }

    public ISysSequence xNormalize()
    {
        return this.normalize();
    }

    public ISysSequence xReverse()
    {
        return this.reverse();
    }

    public boolean xReversed()
    {
        return this.reversed();
    }

    public anytype xReverseStep()
    {
        return this.reverseStep();
    }

    public anytype xStart()
    {
        return this.start();
    }

    public anytype xStep()
    {
        return this.step();
    }

    public static SysSequenceAnytype construct(anytype _start, anytype _end, anytype _step = 1)
    {
        SysSequenceAnytype sequence = new SysSequenceAnytype(_start, _end, _step);

        return sequence;
    }
}