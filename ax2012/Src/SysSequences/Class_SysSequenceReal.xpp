//
// mazzy, https://github.com/mazzy-ax/SysEnumerators
//
public class SysSequenceReal implements ISysSequence
{
    real start;
    real end;
    real step;

    public SysSequenceReal clone()
    {
        SysSequenceReal range = SysSequenceReal::construct(this.start(), this.end(), this.step());

        return range;
    }

    public boolean contains(real value)
    {
        boolean ret = (this.start() <= value && value <= this.end());
        return ret;
    }

    public boolean cover(real value)
    {
        boolean ret = (this.start() < value && value < this.end());
        return ret;
    }

    // переполнение в аксапте не возбуждает исключение, а возвращает значение с отброшенными битами
    public real distance()
    {
        real ret = this.end() - this.start();

        return ret;
    }

    public real end()
    {
        return end;
    }

    public boolean equal(SysSequenceReal sequence)
    {
        boolean ret =   sequence &&
                        this.start() == sequence.start() &&
                        this.end() == sequence.end() &&
                        this.step() == sequence.step();

        return ret;
    }

    public int64 estimatedIterations()
    {
        int64 ret = int64Max();
        Amount distance = this.distance();
        boolean overflow = this.cover(0) && (distance <= 0.00001);

        if( overflow )
        {
            ret = int64Max();
        }
        else if( distance == 0 )
        {
            ret = 1;
        }
        else if( ((distance < 0) && (this.step() > 0))
              || ((distance > 0) && (this.step() < 0)) )
        {
            ret = 0;
        }
        else if( step == 1 )
        {
            ret = any2int64(distance + 1.0);
        }
        else
        {
            ret = any2int64(distance / this.step() + 1.0);
        }

        return ret;
    }

    public SysEnumerator getEnumerator()
    {
        return SysEnumerator::newFromToReal(this.start(), this.end(), this.step());
    }

    public real getNext(real value)
    {
        return value + this.step();
    }

    public boolean in(SysSequenceReal range)
    {
        boolean ret = range && (range.start() <= this.start()) && (this.end() <= range.end());
        return ret;
    }

    public boolean isBoundary(real value)
    {
        return (value == this.start()) || (value == this.end());
    }

    public void new(real _start = 1.0, real _end = RealMax(), real _step = 1.0)
    {
        Debug::assert(_step != 0.0);

        start = _start;
        end = _end;
        step = _step;
    }

    public SysSequenceReal normalize()
    {
        SysSequenceReal sequence;

        if( this.reversed() )
        {
            sequence = this.reverse();
        }
        else
        {
            sequence = this.clone();
        }

        return sequence;
    }

    public SysSequenceReal reverse()
    {
        SysSequenceReal sequence = SysSequenceReal::construct(this.end(), this.start(), this.reverseStep());

        return sequence;
    }

    public boolean reversed()
    {
        boolean ret = this.start() > this.end();
        return ret;
    }

    public real reverseStep()
    {
        return -step;
    }

    public real start()
    {
        return start;
    }

    public real step()
    {
        return step;
    }

    public str toString()
    {
        return strfmt('[%1..%2:%3]', this.start(), this.end(), this.step());
    }

    public ISysSequence xClone()
    {
        return this.clone();
    }

    public boolean xContains(anytype value)
    {
        return this.contains(value);
    }

    public boolean xCover(anytype value)
    {
        return this.cover(value);
    }

    // переполнение в аксапте не возбуждает исключение, а возвращает значение с отброшенными битами
    public anytype xDistance()
    {
        return this.distance();
    }

    public anytype xEnd()
    {
        return this.end();
    }

    public boolean xEqual(ISysSequence range)
    {
        return this.equal(range as SysSequenceReal);
    }

    public anytype xGetNext(anytype value)
    {
        return this.getNext(value);
    }

    public boolean xIn(ISysSequence range)
    {
        return this.in(range as SysSequenceReal);
    }

    public boolean xIsBoundary(anytype value)
    {
        return this.isBoundary(value);
    }

    public ISysSequence xNormalize()
    {
        return this.normalize();
    }

    public ISysSequence xReverse()
    {
        return this.reverse();
    }

    public boolean xReversed()
    {
        return this.reversed();
    }

    public anytype xReverseStep()
    {
        return this.reverseStep();
    }

    public anytype xStart()
    {
        return this.start();
    }

    public anytype xStep()
    {
        return this.step();
    }

    public static SysSequenceReal construct(real _start = 1, real _end = RealMax(), real _step = 1)
    {
        SysSequenceReal sequence = new SysSequenceReal(_start, _end, _step);

        return sequence;
    }
}